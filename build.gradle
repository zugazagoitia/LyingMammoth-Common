plugins {
    id 'java-library'
    id 'maven-publish'
    id 'jacoco'
    id 'io.freefair.lombok' version '6.1.0-m3'
}

group 'com.zugazagoitia.lyingMammoth'
version '1.3'
sourceCompatibility = JavaVersion.VERSION_15
targetCompatibility = JavaVersion.VERSION_15

repositories {
    mavenCentral()
}

java {
    withJavadocJar()
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.7.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.0'

    implementation group: 'com.squareup.moshi', name: 'moshi', version: '1.12.0' // Json Parsing
    implementation 'io.jsonwebtoken:jjwt-api:0.11.2' // Json Signature
    runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.11.2' // Json Signature
}

test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport // report is always generated after tests run
}

jacocoTestReport {
    dependsOn test // tests are required to run before generating the report
    group = "Reporting"
    description = "Generate Jacoco coverage reports after running tests."
    reports {
        xml.required = true
        html.required = false
        xml.outputLocation = layout.buildDirectory.file('reports/jacoco/report.xml')
    }
}
check.dependsOn jacocoTestReport

publishing {
    publications{
        maven(MavenPublication) {
            from(components.java)
        }
    }
    repositories {
        maven {
            name = "main"
            url = "https://repo.zugazagoitia.com/releases/"
            credentials {
                username = System.getenv("MAVEN_USER")
                password = System.getenv("MAVEN_PASS")
            }
            authentication {
                basic(BasicAuthentication)
            }
        }
    }
}